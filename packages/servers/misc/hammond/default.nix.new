{ lib
, buildGoModule
, buildNpmPackage
, yarn
, fetchFromGitHub
}:

buildGoModule rec {
  pname = "hammond";
  version = "2022.07.06";

  src = fetchFromGitHub {
    owner = "akhilrex";
    repo = "hammond";
    rev = "84cba2c7f26f6d3f81c49b132110b24ac97c7b49";
    hash = "sha256-kBL9qRpYvdyt98Jaesk2QLpbGXFTs1oAc6I/v36a4nE=";
  };

  hammond-frontend = buildNpmPackage {
    pname = "hammond-frontend";

    inherit src version meta;

    sourceRoot = "source/ui";

    npmDepsHash = "sha256-W1M6tta56iUn2a+oBW3Qayn9TguBBnNPwaCcblOBGMg=";

    npmFlags = [ "--legacy-peer-deps" ];
    npmRebuildFlags = [ ];
    NODE_OPTIONS = "--openssl-legacy-provider";

    patches = [ ./lock.patch ];

    postPatch = ''
      substituteInPlace package.json --replace '"version": "0.0.0"' '"version": "${version}"'
    '';

    nativeBuildInputs = [ yarn ];

    npmBuildScript = "build:ci";

    postInstall = ''
      mv /build/source/ui/dist $out/lib/node_modules/frontend
    '';
  };

  modRoot = "server";

  vendorSha256 = "sha256-wb36Ppt8ZuH4srfpaCIfYLrk/VQK1esinM778FlfknM=";

  postInstall = ''
    mkdir $out/lib
    cp -r ${hammond-frontend}/lib/node_modules/frontend/dist $out/bin/
  '';

  meta = with lib; {
    description = "Self hosted vehicle and expense management system. Like Clarkson, but better";
    homepage = "https://github.com/akhilrex/hammond";
    license = licenses.agpl3Only;
    maintainers = with maintainers; [ camillemndn ];
    platforms = platforms.linux;
  };
}
